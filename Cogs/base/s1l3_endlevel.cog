# Jedi Knight Cog Script
#
# S1L3_ENDLEVEL.COG
#
# Endlevel cog for level S1L3
#
# [KS]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols

message         startup
message         user0
message     shutdown

sound           dialogue

int             player           local
sound           goalsnd=Accomplish1.wav   local
end

# ========================================================================================

## Code Section
code

startup:
   // This is the master COG, so register it.
   SetMasterCOG(GetSelfCOG());

   player = GetLocalPlayerThing();

   // Setup the goals first string in cogStrings.uni so the UI knows about it.
   SetInv(player, 99, 3000);

   // Setup all the goals as displayed at start
   SetGoalFlags(player, 0, 1);
   SetGoalFlags(player, 1, 1);
   SetGoalFlags(player, 2, 1);

   // Play the movie
   jkStartupCutscene ("s1l3ocs.san");
   Sleep(0.05);

   // Initialize weapon.
   SetFireWait(player, -1);
   SetMountWait(player, 0);
   SetCurInvWeapon(player, 0);
   SelectWeapon(player, GetWeaponBin(AutoSelectWeapon(player, 1)));

   Return;

# ........................................................................................

user0:
   jkPrintUNIString(player, 350);

   if(GetParam(0) == 0)
   {
      PlaySoundThing(goalsnd, player, 1.0, -1, -1, 0);
      SetGoalFlags(player, 0, 2);
      Return;
   }

   if(GetParam(0) == 1)
   {
      PlaySoundThing(goalsnd, player, 1.0, -1, -1, 0);
      SetGoalFlags(player, 1, 2);
      Return;
   }

   if(GetParam(0) == 2)
   {
      SetGoalFlags(player, 2, 2);
      PlaySoundLocal(dialogue, 1, 0, 132);
//      jkPrintUNIString(-1, 42001);    //"I doubt these guys would notice one extra passenger!"

      // Wait a bit before level ends
      Sleep(6.0);
      jkEndLevel(1);
      Return;
   }

   Return;
# ........................................................................................
shutdown:
   ChangeInv(player, 16, 2);
      // Give a force star if the player finds all the secrets
      if((GetInv(player, 71) != 0) && (GetInv(player, 70) == GetInv(player, 71)))
         ChangeInv(player, 16, 1);

   Return;

end
