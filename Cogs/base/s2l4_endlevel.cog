# Jedi Knight Cog Script
#
# S2L4_ENDLEVEL.COG
#
# Endlevel cog for level S2L4
#
# [KS]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols

message         startup
message         user2
message     shutdown
message     timer

sound           dialogue

int             player                 local
sound           goalsnd=Accomplish1.wav      local

#  End level stuff
thing    ship
sound    engine=CrowEngineWhine02.WAV  local
thing    exit_camera
thing    newmara
thing    newmara2
template maramodel
thing    enemy1
thing    enemy2
thing    enemy3
int      mara2          local

#  Rancor eats mara
message     user7
thing    rancoreat
thing    eatcam

model          raSwallow=RAalt.3do        local
model          raNormal=RA.3do         local

keyframe       raChomp=RAchmp.key         local
keyframe ravict=ravict.key    local

template       rancorTmpl=rancor          local

sound          wav0=RancRoar.wav          local
sound          eatmara=RancEatMara.WAV         local

int      newThing       local
int            raThing                         local
int      eaten=0           local

end

# ========================================================================================

## Code Section
code

startup:
   // This is the master COG, so register it.
   SetMasterCOG(GetSelfCOG());

   player = GetLocalPlayerThing();

   // Setup the goals first string in cogStrings.uni so the UI knows about it.
   SetInv(player, 99, 24000);

   AISetMode (rancoreat, 0x2000);

   // Setup all the goals as displayed at start
   SetGoalFlags(player, 0, 1);
   //SetGoalFlags(player, 1, 1);
   SetGoalFlags(player, 2, 1);
   SetGoalFlags(player, 3, 1);

   // This is the prison level; clear Mara's inventory
   SetTimer (0.25);

   // When player starts, give him fists, and a bryar.
   //SetInv(player, 1, 1.0);
   //SetInv(player, 2, 1.0);
   //if(GetInv(player, 11) < 50) SetInv(player, 11, 50.0);

   Sleep(0.05);

   // Initialize weapon.
   SetFireWait(player, -1);
   SetMountWait(player, 0);
   SetCurInvWeapon(player, 0);
   SelectWeapon(player, GetWeaponBin(AutoSelectWeapon(player, 1)));

   Return;

# ........................................................................................

timer:

   // Reset the weapons (this is the prison level)
   SetInv(player, 121, 1.0);
   SetInv(player, 122, 0.0);
   SetInv(player, 123, 0.0);
   SetInv(player, 124, 0.0);
   SetInv(player, 125, 0.0);
   SetInv(player, 126, 0.0);
   SetInv(player, 127, 0.0);
   SetInv(player, 128, 0.0);
   SetInv(player, 129, 0.0);
   SetInv(player, 130, 0.0);

   SetInv(player, 131, 0.0);
   SetInv(player, 132, 0.0);
   SetInv(player, 133, 0.0);
   SetInv(player, 134, 0.0);
   SetInv(player, 135, 0.0);
   SetInv(player, 136, 0.0);
   SetInv(player, 137, 0.0);
   SetInv(player, 138, 0.0);
   SetInv(player, 139, 0.0);
   SetInv(player, 140, 0.0);

   // Reset ammo & energy
   SetInv(player, 11, 0.0);
   SetInvAvailable(player, 11, 0);
   SetInv(player, 12, 0.0);
   SetInv(player, 15, 0.0);
   SetInv(player, 90, 0.0);
   SetInv(player, 91, 0.0);
   SetInv(player, 92, 0.0);

   // Reset inventory
   SetInv(player, 40, 0.0);
   SetInvAvailable(player, 40, 0);
   SetInv(player, 41, 0.0);
   SetInvAvailable(player, 41, 0);
   SetInv(player, 43, 0.0);
   SetInv(player, 44, 0.0);
   SetInv(player, 45, 0.0);
   SetInv(player, 46, 0.0);
   SetInv(player, 47, 0.0);
   SetInv(player, 48, 0.0);
   SetInv(player, 49, 0.0);
   SetInv(player, 50, 0.0);
   SetInv(player, 51, 0.0);
   SetInv(player, 52, 0.0);
   SetInv(player, 53, 0.0);
   SetInv(player, 54, 0.0);
   SetInv(player, 55, 0.0);
   SetInv(player, 56, 0.0);

   // Reset player stats
   SetInv(player, 61, 0.0);
   SetInv(player, 62, 0.0);
   SetInv(player, 64, 0.0);
   SetInv(player, 65, 0.0);
   SetInv(player, 66, 0.0);

   // Initialize weapon.
   SetFireWait(player, -1);
   SetMountWait(player, 0);
   SetCurInvWeapon(player, 0);
   SelectWeapon(player, GetWeaponBin(1));

   return;

# ........................................................................................

user2:
   jkPrintUNIString(player, 350);

   if(GetParam(0) == 0)
   {
      PlaySoundThing(goalsnd, player, 1.0, -1, -1, 0);
      SetGoalFlags(player, 0, 2);
      Return;
   }

   if(GetParam(0) == 1)
   {
      PlaySoundThing(goalsnd, player, 1.0, -1, -1, 0);
      SetGoalFlags(player, 1, 3);
      Return;
   }

   if(GetParam(0) == 2)
   {
      PlaySoundThing(goalsnd, player, 1.0, -1, -1, 0);
      SetGoalFlags(player, 2, 2);
      Return;
   }

   if(GetParam(0) == 3)
   {
      SetGoalFlags(player, 3, 2);
      PlaySoundLocal(goalsnd, 1, 0, 132);

      // Wait a bit before level ends
      Sleep(0.5);
   SetActorFlags(player, 0xa00000);
      SetActorFlags(player, 0x8);
   SetCurrentCamera(0);
   SetCameraFocus(0, exit_camera);
   StopThing(player);
   AISetMode(enemy1, 0x2000);
   AISetMode(enemy2, 0x2000);
   AISetMode(enemy3, 0x2000);
   mara2 = CreateThing(maramodel, newmara);
   AISetMode(mara2, 0x2000);
   AISetMoveSpeed(mara2, 1.5);
   AISetLookPos(mara2, GetThingPos(newmara2));
   PlaySoundThing(engine, ship, 1.3, -1, 225, 129);
   MoveToFrame(exit_camera, 1, 2);
   AISetMovePos(mara2, GetThingPos(newmara2));
   MoveToFrame(ship, 3, 22);
   WaitForStop(ship);
   MoveToFrame(ship, 4, 5);
   WaitForStop(ship);
   Sleep(0.5);
      jkEndLevel(1);
      ClearActorFlags(player, 0x8);
      Return;
   }

   Return;

//-------------------------------------------------------------------------------------------
user7:
// print("Rancor");
   if (eaten) return;
   eaten=1;
      player = GetLocalPlayerThing();
// if(GetParam(0) == 0)
// {
//    raThing = GetParam(0);

        // set the player's health to 1
      SetThingHealth (player, 1);

      // take player control
      player = GetLocalPlayerThing();
      SetActorFlags (player, 0xa00000);
	StopThing(player);
   
   SetCurrentCamera (0);
   SetCameraFocus (0, eatcam);

   Sleep (0.3);

      SetThingModel (rancoreat, raSwallow);

   // stupify the rancor
   AISetMode (rancoreat, 0x2000);

      // play chomp anim
      PlayKey(rancoreat, raChomp, 1, 0x4);
   PlaySoundLocal (eatmara, 1.0, 0.0, 0);
   Sleep(GetKeyLen(raChomp));
   Sleep(0.5);

   // Create a new rancor...
   Sleep (0.3);
      newThing = CreateThing (rancorTmpl, rancoreat);
   DestroyThing (rancoreat);
      raThing= newThing;

   // stupify the rancor
   AISetMode (raThing, 0x2000);

   // play victory
   PlayMode(raThing, 37);
   Sleep(0.5);
   PlaySoundLocal(wav0, 1.0, 0.0, 0);

   Sleep(GetKeyLen(RAvict));
   Sleep(1);
   KillPlayerQuietly();
// JKEndLevel(0);
// }
   return;

# ........................................................................................
shutdown:
   ChangeInv(player, 16, 2);
      // Give a force star if the player finds all the secrets
      if((GetInv(player, 71) != 0) && (GetInv(player, 70) == GetInv(player, 71)))
         ChangeInv(player, 16, 1);

   Return;

end
