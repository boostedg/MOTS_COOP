# Jedi Knight Cog Script
#
# S5L2_forceillusion.cog
#
# An adjoin is "no move" and "always draw" until the player activates "seeing."
#
# [JS]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved
# ========================================================================================

symbols
message	startup		
message	entered
message	pulse		
message	crossed		

surface	wall							linkid=1
surface	wallback
thing		eye_pos0					desc=generator_ghost
thing		eye_pos1					desc=generator_ghost
template	eye_tpl					desc=object_to_generate
sound		wav1						desc=success_alert

int		player					local
int		eye0=-1					local
int		eye1=-1					local
int		trickdone=0				local

sector	alert_sector			desc=alert_sector
	
end

# ========================================================================================

code

startup:
	player = GetLocalPlayerThing();
   ClearAdjoinFlags(wall, 2);				//no move
   ClearAdjoinFlags(wallback, 2);
	return;

# ........................................................................................

entered:
	if(trickdone) Return;

	// do the eyes effects and everything (the player enters the area)...
	eye0=CreateThing(eye_tpl, eye_pos0); 
	eye1=CreateThing(eye_tpl, eye_pos1); 
	SetPulse(0.5);
	
	return;

# ........................................................................................

crossed:
	if(trickdone) Return;
	if(GetSenderId(GetSenderRef()) !=1) return;

	DestroyThing(eye0);
	DestroyThing(eye1);
	SetPulse(0);
	PlaySoundPos(wav1, SurfaceCenter(wallback), 1.5, -1, -1, 0 );

	// remove eyes effects if you want to (the player leaves the area but DID not succeed)...

	return;

# ........................................................................................

pulse:
	if(IsInvActivated(player, 23))	// is Seeing active ?
	{
	   trickdone = 1;
	   SetPulse(0);
	   // remove the eyes, open the doors, whatever you want to do next...
		SetAdjoinFlags(wall, 3);             //move
      SetAdjoinFlags(wallback, 3);
		SetFaceGeoMode(wall, 0);
		SetFaceGeoMode(wallback, 0);
		DestroyThing(eye_pos0);
		DestroyThing(eye_pos1);
	}
	else
	{
	   // booh the player or whatever... :-)
	}
	return;

end

